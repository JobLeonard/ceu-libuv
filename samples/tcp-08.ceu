#if 0
#@ Description: "echo" client & server
#@ Features:
#@  - UV_TCP_Server, UV_TCP_Client
#endif

#include "c.ceu"

#include "uv.ceu"
#define UV_TCP_SERVER_HANDLER_CEU tcp-08-handler.inc
#include "uv-server.ceu"

#define PORT    7000
#define BACKLOG 128

var char[] ip1 = [].."0.0.0.0";
var UV_TCP_Server _ = UV_TCP_Server.build(&ip1, PORT, BACKLOG);

var char[] ip2 = [].."127.0.0.1";
var UV_TCP_Client c = UV_TCP_Client.build(&ip2, PORT);
var int status = await c.ok;
_assert(status == 0);

_printf("I will terminate after 10s...\n");

loop i in 10 do
    await 1s;
    var char[] string = [] .. "Alo mundo!\n";
    do UV_Stream_Write.build(&_UV_STREAM_ALIAS(c.tcp), &string);
    $string = 0;
    do UV_Stream_ReadLine.build(&_UV_STREAM_ALIAS(c.tcp), &string);
    _printf("[client] %s", (_char&&)&&string);
end

_printf("DONE!\n");
escape 0;
